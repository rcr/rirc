%{
#include <string.h>

#define SEND_HANDLERS \
	X(away) \
	X(notice) \
	X(part) \
	X(privmsg) \
	X(quit) \
	X(topic) \
	X(topic_unset)

#define SEND_CTCP_HANDLERS \
	X(action) \
	X(clientinfo) \
	X(finger) \
	X(ping) \
	X(source) \
	X(time) \
	X(userinfo) \
	X(version)

#define SEND_IRCV3_CAP_HANDLERS \
	X(ls) \
	X(list)

#define X(cmd) static int irc_send_##cmd(struct server*, struct channel*, char*);
SEND_HANDLERS
#undef X

#define X(cmd) static int irc_send_ctcp_##cmd(struct server*, struct channel*, char*);
SEND_CTCP_HANDLERS
#undef X

#define X(cmd) static int irc_send_ircv3_cap_##cmd(struct server*, struct channel*, char*);
SEND_IRCV3_CAP_HANDLERS
#undef X

typedef int (*irc_send_f)(struct server*, struct channel*, char*);

struct send_handler
{
	char *key;
	irc_send_f f;
};
%}

%enum
%null-strings
%readonly-tables
%struct-type
%define slot-name key
%define word-array-name      send_handlers
%define hash-function-name   send_handler_hash
%define lookup-function-name send_handler_lookup
%define initializer-suffix ,(irc_send_f)0
struct send_handler;
%%
CAP-LS,          irc_send_ircv3_cap_ls
CAP-LIST,        irc_send_ircv3_cap_list
CTCP-ACTION,     irc_send_ctcp_action
CTCP-CLIENTINFO, irc_send_ctcp_clientinfo
CTCP-FINGER,     irc_send_ctcp_finger
CTCP-PING,       irc_send_ctcp_ping
CTCP-SOURCE,     irc_send_ctcp_source
CTCP-TIME,       irc_send_ctcp_time
CTCP-USERINFO,   irc_send_ctcp_userinfo
CTCP-VERSION,    irc_send_ctcp_version
AWAY,            irc_send_away
NOTICE,          irc_send_notice
PART,            irc_send_part
PRIVMSG,         irc_send_privmsg
QUIT,            irc_send_quit
TOPIC,           irc_send_topic
TOPIC-UNSET,     irc_send_topic_unset
%%
